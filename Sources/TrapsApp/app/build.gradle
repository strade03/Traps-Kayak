plugins {
    id 'com.android.application'
}

android {
    namespace "com.traps.trapsapp"
    compileSdk 33

    defaultConfig {
        applicationId "com.traps.trapsapp"
        minSdk 16
        targetSdk 33
        versionCode 30
        versionName "3.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

   // Ajout pour la signature de l'APK
    // Chargement des propriétés du keystore depuis la racine
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'] ?: 'trapsapp-release-key.keystore')
            storePassword keystoreProperties['storePassword'] ?: System.getenv("MY_KEYSTORE_PASSWORD")
            keyAlias keystoreProperties['keyAlias'] ?: System.getenv("MY_KEY_ALIAS")
            keyPassword keystoreProperties['keyPassword'] ?: System.getenv("MY_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            // Ajout pour signer 
            signingConfig signingConfigs.release // Appliquer la configuration de signature
            shrinkResources true // Supprimer les ressources inutilisées
            zipAlignEnabled true

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.9.0'
    
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
